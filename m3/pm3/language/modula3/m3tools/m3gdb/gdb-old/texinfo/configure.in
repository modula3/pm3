dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.5)
AC_INIT(texinfo.texi)

dnl Checks for programs.
AC_PROG_GCC_TRADITIONAL
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_ISC_POSIX
AC_MINIX

dnl Checks for libraries.
# Needed on sysV68 for sigblock, sigsetmask.
AC_CHECK_LIB(bsd, sigblock)

TERMLIBS=
for termlib in curses termcap terminfo termlib ; do
   AC_CHECK_LIB(${termlib}, tputs,
     [TERMLIBS="${TERMLIBS} -l${termlib}"; break])
done
AC_SUBST(TERMLIBS)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h termio.h string.h varargs.h \
  sys/time.h sys/fcntl.h sys/ttold.h sys/ptem.h sys/file.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_OFF_T
AC_C_CONST
AC_STRUCT_TM

dnl Checks for library functions.
AC_FUNC_ALLOCA

if test "$ac_cv_c_cross" = no; then
  AC_FUNC_SETVBUF_REVERSED
fi

AC_CHECK_FUNCS(setvbuf getcwd bzero bcopy  memset memcpy memmove strchr \
	       strcasecmp strdup vfprintf vsprintf strerror sigprocmask \
	       sigsetmask)

AC_OUTPUT(Makefile libtxi/Makefile makeinfo/Makefile info/Makefile util/Makefile emacs/Makefile $startInlineCygnus cygnus/Makefile $endInlineCygnus)
