CONST 
  Width = 5.5 * Unit.In, 
  Height = 2.75 * Unit.In, 
  BinderW = 1.125 * Unit.In;

PROC TalkLabel(a) IS 
  IF 
    VAR 
      g = R2.PlusX(a, 1), b ~ (396, 198) REL (a, g), 
      c ~ (81, 198) REL (a, g), d ~ (81, 0) REL (a, g), 
      e ~ (40.5, 99) REL (a, g), f ~ (238.5, 99) REL (a, g) 
    IN 
      Width = Geometry.DistX(a, b) AND 
      Height = Geometry.DistY(a, b) AND 
      c HOR b AND 
      d = Geometry.HorVer(a, c) AND 
      BinderW = Geometry.DistX(a, d) AND 
      e = Geometry.Mid(a, c) AND 
      f = Geometry.Mid(d, b) -> 
        Rect.Draw(a, b); 
        PS.Stroke(); 
        SAVE PS IN 
          PS.MoveTo(c); 
          PS.LineTo(d); 
          PS.SetColor(Color.Grey50); 
          PS.Stroke() 
        END; 
        TypeLinesC.Center(e, 
                          "Juno-2\nTalk Tape\n\n" & 
                          "April, 1995\n13:40\nSilent"); 
        TypeLinesL.Center(f, 
                          "Contents:\n\n" & 
                          "1. Equilateral Triangle\n" & 
                          "2. Fold triangle to make diamond\n" & 
                          "3. Block letter \"A\"\n" & 
                          "4. Simple DiGraph\n" & 
                          "5. Tetrahedron\n" & 
                          "6. Letter \"S\"\n" & 
                          "7. Calligraphic \"S\"\n" & 
                          "8. Animations (x5)") 
    END 
  FI 
END;

UI PointTool(TalkLabel);

PROC Cmd0() IS 
  IF 
    VAR a ~ (-202.22096, 59.162155), b ~ (-202.22096, -213.13545) IN 
      a VER b -> 
        PS.SetFont("Times-Bold", PS.Big); TalkLabel(a); TalkLabel(b) 
    END 
  FI 
END;
