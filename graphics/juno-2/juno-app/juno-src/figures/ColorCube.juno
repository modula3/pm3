CONST 
  Grain = 4, 
  Delta = 1 / Grain, 
  VerGap = 5;

FUNC r = Coord(a, b, x, y) IS 
  r = 
    R2.Plus(a, (x * CAR(b), y * CDR(b))) 
END;

PROC DrawSquare(a, delta, r, g, b) IS 
  VAR c1, c2 IN 
    c1 := Coord(a, delta, r, g); 
    c2 := 
      Coord(a, delta, r + Delta, 
            g + Delta); 
    Rect.Draw(c1, c2); 
    SAVE PS IN 
      PS.SetColor(Color.FromRGB(r, g, b)); 
      PS.Fill() 
    END; 
    PS.Stroke() 
  END 
END;

PROC DrawLayer(a, b, blue) IS 
  IF 
    VAR 
      dab 
    = R2.Times(Grain / (Grain + 1), 
               R2.Minus(b, a)), r = 0, g 
    IN 
      DO 
        r <= 1 -> 
          g := 0; 
          DO 
            g <= 1 -> 
              DrawSquare(a, dab, r, g, 
                         blue); 
              g := g + Delta 
          OD; 
          r := r + Delta 
      OD 
    END 
  FI 
END;

PROC DrawCube(a, b) IS 
  IF 
    VAR 
      blue = 0, ba = R2.Minus(b, a), 
      dy = CDR(ba) + VerGap 
    IN 
      a := 
        R2.MinusY(a, 
                  dy * (1 / Delta) / 2); 
      DO 
        blue <= 1 -> 
          b := R2.Plus(a, ba); 
          DrawLayer(a, b, blue); 
          VAR c, d IN 
            c := Geometry.HorVer(a, b); 
            d := Geometry.Mid(b, c); 
            TypeLinesL.West(R2.Plus(d, 
                                    (5, 
                                     0)), 
                            "Blue = " & 
                            Unparse.Value(blue)) 
          END; 
          a := R2.PlusY(a, dy); 
          blue := blue + Delta 
      OD 
    END 
  FI 
END;

PROC Cmd0() IS 
  IF 
    VAR a ~ (-69.54, -37.92), b ~ (29.54, 37.92), c = (-20, 0) IN 
      c = Geometry.Mid(a, b) -> PS.SetWidth(1.5); DrawCube(a, b) 
    END 
  FI 
END;
